# 数値と文字列

## Perlにおける数値

PerlではCやPythonの用に**データ型の様なものが存在しません**。
つまりPerlではある数を数値として使いたい時は数値として、文字列として使いたい場合は文字列として解釈されます。
例えば`$number = 10;`として「 `10 + $number` 」として使う場合は数値ですが、「 `print "This is a $number\n";`」として利用する場合は文字列として解釈されます。


この性質をPerlでは**コンテキスト**と呼び、以降様々なコンテキストが出現します。
先程の文字列としての解釈は「文字列コンテキスト (string context)」、数値としての解釈は「数値コンテキスト (numeric context)」などと呼ばれます。

### 数値への演算子

それでは次に, Perlで利用可能な, 数値に対する演算子を見ていきましょう.
これらの演算子を使うことで, 変数に格納された数値に対して, 様々な計算を行うことができます.

```perl
use strict;
use warnings;

my $foo = 4;
my $bar = 2;
my $sum = $foo + $bar;  # 4 + 2 = 6
my $sub = $foo - $bar;  # 4 - 2 = 2
my $mul = $foo * $bar;  # 4 * 2 = 8
my $div = $foo / $bar;  # 4 / 2 = 2
my $mod = $foo % $bar;  # 4 % 2 = 0
my $exp = $foo ** $bar; # 4の2乗は 16
```

このコードでは, 2つの変数`$foo`と`$bar`に数値を代入し, それらを使って`+`演算子で加算(足し算), `-`演算子で減算(引き算), `*`演算子で乗算(掛け算), `/`で除算(割り算), `%`演算子で剰余(左辺を右辺で割った余り), `**`演算子で冪乗(べき乗, 左辺の数値の右辺の数値乗)を計算しています.

なお, これらの演算子のうち, `/`演算子と`%`演算子については, 右辺の値(割る値)を0にすると, 実行時にエラーとなります.

```perl
use strict;
use warnings;

my $div = 1 / 0;
```

このコードの実行結果は, 次の通りです.

```
Illegal division by zero at prog.pl line 4.
```

### 数値への演算子(省略記法)

先程の様な計算はプログラミングをする上で多く利用する為,省略記法を用いる事ができます.

```perl
my $fuga = 100;
my $hoge = 50;

$fuga += 1;      # $fuga = $fuga + 1;と同じ
$fuga -= 1;      # $fuga = $fuga - 1;と同じ
$fuga *= 2;      # $fuga = $fuga * 2;と同じ
$fuga /= $hoge;  # $fuga = $fuga / $hoge;と同じ
```

### インクリメント / デクリメント

```perl
use strict;
use warnings;

my $foo = 4;

$foo++;     # (1) $fooをインクリメント
print $foo; # => 5

$foo--;     # (2) $fooをデクリメント
print $foo; # => 4
```

変数に格納された数値を, 1ずつ増やしたい/減らしたいときは, インクリメント/デクリメントという演算子を利用することができます.
(1)のように, 変数の後ろにインクリメント演算子(`++`)を書くと, その変数に格納された数値が1増えます.
同様に, (2)のように, 変数の後ろにデクリメント演算子(`--`)を書くと, その変数に格納された数値が1減ります.
また、1ずつ増やす事をインクリメント,減らす事をデクリメントと呼びます.

## Perlにおける文字列

文字列とは数値とは別の**文字の並び**です。例えば「`"Perl"` 」や 「`"Perl入学式"` 」などが該当します。
プログラミングをしていく上で文字列を利用したくなることがあるでしょう。
Perlでは他の言語と同じくシングルクォート(`'`)かダブルクォート(`"`)で文字を囲むと「文字列リテラル」と解釈され、文字列として利用できます。
この文字列リテラルはスカラー変数(`$`から始まる変数)代入する事が可能です。

print文など「文字列コンテキスト」で評価をさせる場合ほとんどの変数がPerl側で文字列として解釈されます。


### 数値への演算子

続いて, Perlで利用可能な, 文字列に対する演算子を見ていきましょう.

```perl
use strict;
use warnings;

my $foo = 'foo';
my $bar = 'bar';
my $baz = 1;

print $foo . $bar;  # => foobar (1)
print $foo . 'oof'; # => foooof (2)
print $foo . $baz;  # => foo1   (3)
print $foo . "\n";  # => foo\n  (4)
print $foo . $bar . $baz; # => foobar1 (5)

my $string = $foo . $bar; (1')
```

まずは, 文字列を連結する`.`演算子です. Perlで文字列を出力するとき, 非常によく利用する演算子です.
`.`演算子を使えば,  `(1)`や`(2)`のように, 変数に格納された文字列や, 文字列を自由に連結することができます.
もちろん, `(1')`のように, 連結した文字列を変数に代入することも可能です.

前述したように, Perlでは数値を文字列としてそのまま利用することができます. そのため, (3)のように, 文字列が代入された変数(`$foo`)と, 数値が代入された変数(`$baz`)を, `.`で連結することが出来ます. この場合, `$foo`に格納された`foo`と, `$baz`に格納された`1`が連結された, `foo1`という文字列を得ることができます.

変数を出力する際, 末尾に改行を付与したい時は, `(5)`のように書くことが出来ます. `.`演算子を使って, 変数`$foo`と改行を表す`\n`を連結しています.

最後に, `(5)`のように`.`演算子を複数回使って, 複数の変数や文字列を一気に連結することも可能です.


```perl
use strict;
use warnings;

my $foo = 'foo' x 3;
print $foo; # => foofoofoo
```

次に紹介するのは, 文字列を任意の回数繰り返す`x`です.
例えば上記のコードは, `foo`という文字列を3回繰り返した文字列, つまり`foofoofoo`という文字列を, 変数`$foo`に代入しています. 

## 演算の順番

演算子を使って変数などを操作するとき, その優先順位について気をつける必要があります.
数学で, `( ... )`を使って演算の順序を指定できるように, Perlでも`( ... )`を使って演算の順序を変更することができます.

```perl
use strict;
use warnings;

my $foo =  2 + 4  * 3; # => 14 (1)
my $bar = (2 + 4) * 3; # => 18 (2)
```

四則演算に関する演算子の優先順序は, 基本的に一般的な数学と同じ(加算, 減算よりも乗算, 除算を先に行う)です.
`(1)`は先に`4 * 3`の計算が行われ, その後に`2`が足されるので, 計算結果は`14`になりますが, `(2)`では`(2 + 4)`で, この足し算を先に行うように指定しているので, 結果として`6 * 3`が計算され, 計算結果は`18`になります.

### よくある間違い: 数値の演算と文字列連結

```perl
use strict;
use warnings;

my $now = 2018;

print "Last year it was " . $now - 1 . ". Now it is " . $now; 
```

このコードは, 変数`$now`に代入した年(今年は2018年です)を利用して, 去年の年と, 今年の年を出力するコードです.
`Last year it was 2017. Now it is 2018`という出力を期待したいところですが, 実際にこのコードを実行すると, 次のような警告が表示され, 更に出力は`-1. Now it is 2018`となってしまっています.

```
Argument "Last year it was 2018" isn't numeric in subtraction (-) at prog.pl line 6.
-1. Now it is 2018
```

これは何故でしょうか? ...結論から述べると, `.`と`-`の演算の優先順位が, Perlにおいては同じだからです.
演算子の優先順位が同じ場合, Perlはその演算子が｢左結合｣か｢右結合｣かによって, どちらを先に実行するかを定めます.

`.`と`-`, そして`+`の演算子は優先順位が同じで, 左結合の演算子となっています.
そのため, このコードを実行すると,  `$now - 1`ではなく, 先に一番左側にある`Last year it was `という文字列と変数`$now`の連結を行い, その結果である`Last year it was 2018`という文字列から, `1`を引くという順序で計算を行います.

この時Perlは, `Last year it was 2018`という文字列を, (先程示したように, 警告は表示するものの)無理矢理数値として解釈して計算を行い, コードの処理を継続します.
`Last year it was 2017`を数値として解釈すると, Perlでは`0`になるので, `0 - 1`すなわち`-1`が表示されていたというわけです.

これを防ぐには, 次のコードのように, `$now - 1`を`( ...  )`で囲い, 先に計算させるという方法があります.

```perl
use strict;
use warnings;

my $now = 2018;

print "Last year it was " . ($now - 1) . ". Now it is " . $now . "\n"; 
```

また, 次のように, 先に1年前の年を計算しておくという方法もあります.

```perl
use strict;
use warnings;

my $now = 2018;
my $last_year = $now - 1;

print "Last year it was " . $last_year . ". Now it is " . $now . "\n"; 
```

なお, 演算子の優先順位や, 左結合か右結合かなどについては, Perldocの`perlop`に記載されています([英語](http://search.cpan.org/~shay/perl-5.26.1/pod/perlop.pod) / [日本語](http://perldoc.jp/docs/perl/5.22.1/perlop.pod)).

## 代入演算子

最後に, 改めて代入演算子について触れておきます.

代入演算子 `=` は **右辺を左辺に代入する** 演算子です。
その為右辺で同じ値を利用することが可能です。

```perl
$num = 30;
$num = $num + $num;
```

そして代入演算子は省略することが可能です。

```perl
$num = 30;
$num *= 30;
```

では、次の結果は何になるでしょうか。考えてみましょう。

```perl
$num = 3;
$num = $num * 30;
$num /= 10;
```

## 練習問題 2-3

標準入力から0以外の整数を2つ読み込み, それらを四則演算(`+`, `-`, `*`. `/`)した結果を出力するコードを書いてみましょう

例えば, 標準入力で`1`と`2`を入力した場合, 次のようになります:

```
1 + 2 = 3
1 - 2 = -1
1 * 2 = 2
1 / 2 = 0.5
```
